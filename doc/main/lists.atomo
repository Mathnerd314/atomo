#title:{Lists}tag:"lists"

#define:{
    List
    > Object
}body:{
    Lists are mutable, growable vectors of arbitrary values.

    #example-segment:({
        [1, '2', "three", 4]
        []
    })
    
    Like objects, they are reference values:

    #example:({
        a = [1, 2]
        b = a
        b << 3
        a
    })
}

#define:{
    l copy
    | l is-a?: List
    > List
}body:{
    Copy a list, yielding a new #atomo:List containing the same values as #atomo:l.

    #example:({
        a = [1, 2]
        b = a copy
        b << 3
        a
    })
}

#define:{
    l length
    | l is-a?: List
    > Integer
}body:{
    Returns the length of the list #atomo:l.

    #example:({
        [1, 2, 3] length
    })
}

#define:{
    l empty?
    | l is-a?: List
    > Boolean
}body:{
    Is the list #atomo:l empty?

    #example:({
        [] empty?
        [1, 2, 3] empty?
    })
}

#define:{
    l copy
    > List
}body:{
    Returns a new #atomo:List with the same contents as #atomo:l.
}

#define:{
    l at: index
    | l is-a?: List
    | index is-a?: Integer
    > any
}body:{
    Returns the list's value at #atomo:index. If the index is out of bounds, #atomo:@(out-of-bounds: index for-list: l) is raised.

    #example:({
        [1, 2, 3] at: 1
        [1, 2, 3] at: 3
    })
}

#define:{
    l at: index put: v
    | l is-a?: List
    | index is-a?: Integer
    > List
}body:{
    Replace the value at #atomo:index with #atomo:v. If the index is out of bounds, #atomo:@(out-of-bounds: index for-list: l) is raised.

    #example:({
        numbers = [1, 2, 3]
        numbers at: 1 put: 5
        numbers
    })
}

#define:{
    l head
    | l is-a?: List
    > any
}body:{
    Returns the first value of the list. If the list is empty, #atomo:(@empty-list) is raised.

    #example:({
        [1, 2, 3] head
    })
}

#define:{
    l last
    | l is-a?: List
    > any
}body:{
    Returns the last value of the list. If the list is empty, #atomo:(@empty-list) is raised.

    #example:({
        [1, 2, 3] last
    })
}

#define:{
    l init
    | l is-a?: List
    > List
}body:{
    Returns all but the last value of the list. If the list is empty, #atomo:(@empty-list) is raised.

    #example:({
        [1, 2, 3] init
    })
}

#define:{
    l tail
    | l is-a?: List
    > List
}body:{
    Returns all but the first value of the list. If the list is empty, #atomo:(@empty-list) is raised.

    #example:({
        [1, 2, 3] tail
    })
}

#define:{
    l from: start to: end
    | l is-a?: List
    | start is-a?: Integer
    | end is-a?: Integer
    > List
}body:{
    Returns the slice of elements from #atomo:start to #atomo:end of #atomo:l. If the range is invalid, #atomo:@(invalid-range: @(from: start to: end) for-list: l) is raised. A range is invalid if either #atomo:start or #atomo:end are negative, or if either are out of the list's boundaries.

    #example:({
        [1, 2, 3, 4, 5] from: 1 to: 3
    })
}

#define:{
    l take: n
    | l is-a?: List
    | n is-a?: Integer
    > List
}body:{
    Returns a list of the first #atomo:n values of the list. If #atomo:n is greater than the list's length, it returns as many as it can (i.e., the entire list); the length of the returned list will not always be #atomo:n.

    #example:({
        [1, 2, 3, 4, 5] take: 2
        [1, 2] take: 4
    })
}

#define:{
    l drop: n
    | l is-a?: List
    | n is-a?: Integer
    > List
}body:{
    Returns a list of the values after #atomo:n values have been dropped from the start of the list. If #atomo:n is greater than the list's length, it returns an empty list.

    #example:({
        [1, 2, 3, 4, 5] drop: 2
        [5, 6, 7] drop: 5
    })
}

#define:{
    v replicate: n
    | n is-a?: Integer
    > List
}body:{
    Returns a list containing #atomo:v replicated #atomo:n times.

    #example:({
        "Odelay!" replicate: 5
    })
}

#define:{
    b repeat: n
    | b responds-to?: @call
    | n is-a?: Integer
    > List
}body:{
    Returns a list containing the results of #atomo:n #atomo:(call)s of #atomo:b.

    #example:({
        with-input-from: "doc/main/lists.atomo" do: {
            { read-line } repeat: 3
        }
    })
    
    Here we're reading 3 lines of text from the file #atomo:"doc/main/lists.atomo" (the source for the documentation you're reading).
}

#define:{
    a .. b
    | a is-a?: List
    | b is-a?: List
    > List
}body:{
    Appends the two lists #atomo:a and #atomo:b, returning a new list.

    #example:({
        [1, 2, 3] .. [4, 5, 6]
        [] .. [1, 2] .. []
    })
}

#define:{
    l reverse
    | l is-a?: List
    > List
}body:{
    Returns a new list, with all values of #atomo:l, in reversed order.

    #example:({
        [1, 2, 3] reverse
    })
}

#define:{
    l map: a
    | l is-a?: List
    | a responds-to?: @call:
    > List
}body:{
    Returns a new list, with #atomo:(call:) sent to #atomo:a with each value in #atomo:l.

    #example:({
        [1, 2, 3] map: { n | n + 1 }
        [1, 2, 3] map: @(* 2)
    })
}

#define:{
    x zip: y
    | x is-a?: List
    | y is-a?: List
    > List
}body:{
    Yields #atomo:(x zip: y with: @->), i.e. an association list.
}

#define:{
    x zip: y with: a
    | x is-a?: List
    | y is-a?: List
    | a responds-to?: @call:
    > List
}body:{
    "Zips" up the two lists #atomo:x and #atomo:y but calling #atomo:a with the respective values of each list.

    The list it returns is as long as the shorter list.

    #example:({
        [1, 2, 3] zip: [4, 5, 6] with: { a b | a * b }
        [1, 7, 4] zip: [4, 5, 6, 0] with: @min:
    })
}

#define:{
    l filter: p
    | l is-a?: List
    | p responds-to?: @call:
    > List
}body:{
    Returns a list of all values in #atomo:l that satisfy the predicate #atomo:p.

    #example:({
        [1, 2, 3, 4, 5] filter: @odd?
        [1, 2, 3, 2, 1] filter: { n | n == 3 }
    })
}

#define:{
    l reduce: b
    | l is-a?: List
    | b responds-to?: @call:
    > any
}body:{
    Alias for #atomo:(l tail reduce: b with: l head). If the list is empty, #atomo:(@empty-list) is raised.

    #example:({
        [1, 2, 3, 4, 5] reduce: @*
    })
}

#define:{
    l reduce: b with: v
    | l is-a?: List
    | b responds-to?: @call:
    > any
}body:{
    Reduces the list to a single value by "folding" over it, with #atomo:v as the initial value.

    #example:({
        [1, 2, 3, 4, 5] reduce: @*
        [] reduce: @* with: 1
    })
}

#define:{
    l reduce-right: b
    | l is-a?: List
    | b responds-to?: @call:
    > any
}body:{
    Similar to #atomo:(reduce:), but right-associative. If the list is empty, #atomo:(@empty-list) is raised.
}

#define:{
    l reduce-right: b with: v
    | l is-a?: List
    | b responds-to?: @call:
    > any
}body:{
    Similar to #atomo:(reduce:with:), but right-associative.
}

#define:{
    l concat
    | l is-a?: List
    > List
}body:{
    Reduces a list by appending all of its values. The list's values must all be lists.

    #example:({
        [[1, 2], [3, 4, 5], [6]] concat
        [] concat
    })
}

#define:{
    l sum
    | l is-a?: List
    > any
}body:{
    Reduces a list with #atomo:(+) and #atomo:0.

    #example:({
        [1, 2, 3] sum
        [] sum
    })
}

#define:{
    l product
    | l is-a?: List
    > any
}body:{
    Reduces a list with #atomo:(*) and #atomo:1.

    #example:({
        [1, 2, 3] product
        [] product
    })
}

#define:{
    l maximum
    | l is-a?: List
    > any
}body:{
    Reduces a list with #atomo:(max:). The list must not be empty.

    #example:({
        [1, 2, 3] maximum
    })
}

#define:{
    l minimum
    | l is-a?: List
    > any
}body:{
    Reduces a list with #atomo:(min:). The list must not be empty.

    #example:({
        [1, 2, 3] minimum
    })
}

#define:{
    l all?: p
    | l is-a?: List
    | p responds-to?: @call:
    > Boolean
}body:{
    Checks if all values in #atomo:l satisfy the predicate #atomo:p.

    #example:({
        [1, 3, 5] all?: @odd?
        [1, 3, 6] all?: @odd?
        [] all?: @odd?
    })
}

#define:{
    l any?: p
    | l is-a?: List
    | p responds-to?: @call:
    > Boolean
}body:{
    Checks if any values in #atomo:l satisfy the predicate #atomo:p.

    #example:({
        [2, 3, 6] any?: @odd?
        [2, 4, 6] any?: @odd?
        [] any?: @odd?
    })
}

#define:{
    l and
    | l is-a?: List
    > Boolean
}body:{
    Check if all values in the list are #atomo:True.

    #example:({
        [1 == 1, 'a' == 'a'] and
        [1 == 1, 'a' == 'b'] and
        [] and
    })
}

#define:{
    l or
    | l is-a?: List
    > Boolean
}body:{
    Check if any values in the list are #atomo:True.

    #example:({
        [1 == 1, 'a' == 'a'] or
        [1 == 1, 'a' == 'b'] or
        [1 == 2, 'a' == 'c'] or
        [] or
    })
}

#define:{
    l contains?: v
    | l is-a?: List
    > Boolean
}body:{
    Check if #atomo:v is a member of the list #atomo:l.

    #example:({
        "abc" contains?: 'a'
        "abc" contains?: 'd'
    })
}

#define:{
    v in?: l
    | l is-a?: List
    > Boolean
}body:{
    Alias for #atomo:(l contains?: v).
}

#define:{
    start to: end by: diff
    | start is-a?: Integer
    | end is-a?: Integer
    | diff is-a?: Integer
    > List
}body:{
    Efficiently generate a list from the range #atomo:start to #atomo:end, inclusive, by adding #atomo:diff to each integer.

    #example:({
        0 to: 5 by: 2
    })
}

#define:{
    start up-to: end
    | start is-a?: Integer
    | end is-a?: Integer
    > List
}body:{
    Alias for #atomo:(start to: end by: 1).
}

#define:{
    start down-to: end
    | start is-a?: Integer
    | end is-a?: Integer
    > List
}body:{
    Alias for #atomo:(start to: end by: -1).
}

#define:{
    start .. end
    | start is-a?: Integer
    | end is-a?: Integer
    > List
}body:{
    Efficiently generate a sequence of integers between #atomo:start and #atomo:end, inclusive.

    #example:({
        0 .. 5
        5 .. 0
    })
}

#define:{
    start ... end
    | start is-a?: Integer
    | end is-a?: Integer
    > List
}body:{
    Efficiently generate a sequence of integers between #atomo:start and #atomo:end, exclusive.

    #example:({
        0 ... 5
        5 ... 0
    })
}

#define:{
    l << v
    | l is-a?: List
    > List
}body:{
    Pushes a value onto the end of a list, updating it in-place.

    #example:({
        a = [1, 2, 3]
        a << 4
        a
    })
}

#define:{
    v >> l
    | l is-a?: List
    > List
}body:{
    Pushes a value onto the front of a list, updating it in-place.

    #example:({
        a = [1, 2, 3]
        4 >> a
        a
    })
}

#define:{
    l pop!
    | l is-a?: List
    > List
}body:{
    Removes the first value of a list, updating it in-place and returning the popped value. If the list is empty, #atomo:(@empty-list) is raised.

    #example:({
        a = [1, 2, 3]
        a pop!
        a
    })
}

#define:{
    l split: delimiter
    | l is-a?: List
    | delimiter is-a?: List
    > List
}body:{
    Split a list into sublists, separated by #atomo:delimiter. Note that strict equality is used for matching the delimiter; it is not an #atomo:(==) dispatch.

    #example:({
        [0, 1, 1, 2, 1, 1, 3] split: [1, 1]
        [0, 1, 1] split: [1, 1]
        [] split: [1, 1]
    })
}

#define:{
    l split-on: o
    | l is-a?: List
    > List
}body:{
    Split #atomo:l into sublists, breaking on every occurrence of #atomo:o. Also uses strict equality.

    #example:({
        [0, 1, 2, 1, 3] split-on: 1
        [0, 1] split-on: 1
        [] split-on: 1
    })
}

#define:{
    l sort
    | l is-a?: List
    > List
}body:{
    Returns a sorted list of #atomo:l's values, using the mergesort algorithm. It does this by sending #atomo:(>) to determine the larger between two values.

    #example:({
        [1, 6, 3, 0, 10] sort
        ['z', 'f', 'x'] sort
    })
}

#define:{
    l sort-by: compare
    | l is-a?: List
    | compare responds-to?: @call:
    > List
}body:{
    Returns a sorted list of #atomo:l's values, using the mergesort algorithm. #atomo:compare, when #atomo:(call:)ed with two values in #atomo:l, should return #atomo:True if the first is greater than the second, and #atomo:False otherwise.

    #example:({
        [1, 6, 3, 0, 10] sort-by: @>
        [1, 6, 3, 0, 10] sort-by: @<
    })
}
