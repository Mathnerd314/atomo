{ define: *handlers* as: []
  define: *restarts* as: []

  (super) Error = Object clone

  for-macro Handler = Object clone do: { handle: _ := @ok }
  for-macro Restart = Object clone

  Restart new: (a: Block) in: (c: Continuation) :=
    Restart clone do:
      { jump: as := c yield: (a call: as)
      }

  macro action with-restarts: (restarts: Block) :=
    { rs = restarts contents map:
        { `(~n -> ~e) |
          e type match: {
            @block -> `(~n -> Restart new: ~e in: cc)
            _ -> `(~n -> Restart new: { ~e } in: cc)
          }
        }

      `({ cc | ~action with-restarts: ~(`List new: rs) } call/cc)
    } call

  (action: Block) with-restarts: (restarts: List) :=
    with: *restarts* as: (restarts .. *restarts* _?) do: action

  (super) signal: v :=
    { *handlers* _? map: @(handle: v)
      @ok
    } call

  (super) error: e :=
    { err =
        if: (e is-a?: Error)
          then: { e }
          else: { e clone do: { delegates-to: Error } }

      signal: err

      with-output-to: Port standard-error do: {
        '-' (repeat: 78) print
        ("*** " .. e show) print

        when: *restarts* _? empty?
          do: { halt }

        "" print
        "restarts:" print

        *restarts* _? (zip: (0 .. *restarts* _? length)) map:
          { choice |
            [index, name] = [choice to, choice from from]
            ("  " .. index show .. ": " .. name show) print
          }
      }

      chosen = *restarts* _? (at: read) to
      chosen jump: []
    } call

  (super) restart: name := restart: name with: []

  (super) restart: name with: (params: List) :=
    *restarts* _? (lookup: name) match: {
      @(ok: r) -> chosen jump: params
      @none -> error: @(unknown-restart: name)
    }

  (a: Block) bind: (bs: Block) :=
    { h = Handler clone

      bs contents map:
        { `(~c -> ~e) |
          if: (e type == @block)
            then: { @handle: define-on: h with: [`(e: ~(c evaluate-in: sender)) as: Pattern] as: `(~e call: [e]) in: sender }
            else: { @handle: define-on: h with: [c as: Pattern] as: e in: sender }
        }

      with: *handlers* as: (h . *handlers* _?) do: a
    } call
} call
